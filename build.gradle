import dependencies.Dependencies
import dependencies.Versions

buildscript { 
    ext.kotlin_version = '1.3.50'


    repositories {
        google()
        jcenter()
        mavenCentral()
        
    }
    dependencies {
        classpath BuildPlugins.GRADLE
        classpath BuildPlugins.KOTLIN
        classpath BuildPlugins.NAVIGATION
        classpath BuildPlugins.JACOCO
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}


subprojects {

    apply from: "$rootDir/tools/ktlint.gradle"
    apply plugin: 'jacoco'

    afterEvaluate {
        if (!project.hasProperty("android")) return

        android {
            compileSdkVersion AndroidConfig.COMPILE_SDK

            defaultConfig {
                minSdkVersion AndroidConfig.MIN_SDK
                targetSdkVersion AndroidConfig.TARGET_SDK
                versionCode AndroidConfig.VERSION_CODE
                versionName AndroidConfig.VERSION_NAME
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility AndroidConfig.JAVA_VERSION
                targetCompatibility AndroidConfig.JAVA_VERSION
            }

            buildTypes {
                debug {
                    testCoverageEnabled true
                }
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            sourceSets {
                main.java.srcDirs += Source.MAIN

                test.java.srcDirs += [Source.TEST, Source.SHARED_TEST]
                test.resources.srcDirs  += [Source.SHARED_TEST_RESOURCES]

                androidTest {
                    java.srcDirs += [Source.ANDROID_TEST, Source.SHARED_TEST]
                    assets.srcDirs  += [Source.SHARED_TEST_RESOURCES]
                }
            }

            testOptions {
                unitTests.includeAndroidResources = true
                animationsDisabled = true
            }

            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                kotlinOptions {
                    jvmTarget = AndroidConfig.JAVA_VERSION
                }
            }
        }

        jacoco {
            toolVersion = Versions.JACOCO
        }

        dependencies {
            implementation Dependencies.KOTLIN
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
